buildscript {
    ext {
        javaVersion = JavaVersion.VERSION_1_8
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id "java"
    id "checkstyle"
    id "findbugs"
    id "jacoco"
    id "eclipse"
    id "idea"
    id "org.flywaydb.flyway" version "3.2.1"
}

apply plugin: 'spring-boot'

group = 'org.yukung.yokohamagroovy'

targetCompatibility = sourceCompatibility = javaVersion
tasks.withType(AbstractCompile)*.options*.encoding = 'UTF-8'

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

repositories {
    mavenCentral()
    maven { url "http://public-maven.classmethod.info/release" }
    maven { url "http://public-maven.classmethod.info/snapshot" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-devtools"
    compile "org.springframework.boot:spring-boot-starter-jdbc"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.flywaydb:flyway-core"
    provided "org.projectlombok:lombok:1.16.6"
    compile "org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16"
    runtime "com.h2database:h2"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.dbunit:dbunit:2.4.9"
    testCompile "jp.classmethod.testing:cmtest-db:0.4"
}

checkstyle {
    toolVersion = '6.12.1'
}

findbugs {
    reportLevel = 'medium'
}

flyway {
    url = "jdbc:h2:file:./build/libraries"
    user = "sa"
    password = ""
}

jar {
    baseName = 'libraries'
    version = '0.0.1-SNAPSHOT'
}

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
        vcs = 'Git'
    }
    module {
        scopes.PROVIDED.plus += [configurations.provided]
        downloadJavadoc = true
        downloadSources = true
    }
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
        plusConfigurations += [configurations.provided]
        downloadJavadoc = true
        downloadSources = true
    }
}

// テストコードの checkstyle, findbugs の検査はパスする
task checkstyleTest(overwrite: true) << {}
task findbugsTest(overwrite: true) << {}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
